plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = 'UTF-8'

archivesBaseName = project.archives_base_name + "-spigot"
version = project.version
group = project.maven_group

repositories {
    mavenCentral()

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation "org.bstats:bstats-bukkit:${project.bstats_version}"

    compileOnly "org.spigotmc:spigot-api:${project.spigot_version}"
}

processResources {
     filesMatching("plugin.yml") {
         expand "version": project.version
     }
}

shadowJar {
    relocate 'org.bstats', 'de.rafael.plugins.better.farmland.utils'
}

assemble.dependsOn shadowJar

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.archives_base_name + "-spigot"
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            allowInsecureProtocol = true
            url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }
        }
    }
}